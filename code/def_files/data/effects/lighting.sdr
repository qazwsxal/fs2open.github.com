//? #version 150
const int LT_DIRECTIONAL	= 0;		// A light like a sun
const int LT_POINT			= 1;		// A point light, like an explosion
const int LT_TUBE			= 2;		// A tube light, like a fluorescent light
const int LT_CONE			= 3;		// A cone light, like a flood light

const float SPEC_FACTOR_NO_SPEC_MAP		= 0.1;
const float GLOW_MAP_INTENSITY			= 1.5;
const float GLOW_MAP_SRGB_MULTIPLIER	= 3.0;
const float PI = 3.14159f;

#ifdef FLAG_ENV_MAP
// Construct Hammersley low discrepancy sequence for reduced noise/variance in Envmap sampling.
const uint NUM_SAMPLES = 32;
const vec2 CIRCLE_POINTS[32] = vec2[32](
vec2( 0.0000, 0.0000),
vec2(-0.1768, 0.0000),
vec2( 0.0000, 0.2500),
vec2(-0.0000,-0.3062),
vec2( 0.2500, 0.2500),
vec2(-0.2795,-0.2795),
vec2(-0.3062, 0.3062),
vec2( 0.3307,-0.3307),
vec2( 0.4619, 0.1913),
vec2(-0.4900,-0.2029),
vec2(-0.2139, 0.5165),
vec2( 0.2244,-0.5417),
vec2( 0.2343, 0.5658),
vec2(-0.2439,-0.5889),
vec2(-0.6111, 0.2531),
vec2( 0.6325,-0.2620),
vec2( 0.6935, 0.1379),
vec2(-0.7149,-0.1422),
vec2(-0.1463, 0.7356),
vec2( 0.1503,-0.7557),
vec2( 0.4392, 0.6573),
vec2(-0.4501,-0.6736),
vec2(-0.6894, 0.4607),
vec2( 0.7049,-0.4710),
vec2( 0.7201, 0.4811),
vec2(-0.7349,-0.4911),
vec2(-0.5008, 0.7495),
vec2( 0.5103,-0.7638),
vec2( 0.1825, 0.9174),
vec2(-0.1857,-0.9337),
vec2(-0.9496, 0.1889),
vec2( 0.9653,-0.1920)
);
#endif

vec3 FresnelSchlick(vec3 halfVec, vec3 view, vec3 specColor)
{
	return specColor + (vec3(1.0) - specColor) * pow(1.0 - clamp(dot(view, halfVec), 0.0, 1.0), 5.0);
}

vec3 SpecularBlinnPhong(vec3 specColor, vec3 light, vec3 normal, vec3 halfVec, float specPower, float fresnel, float dotNL)
{
	return mix(specColor, FresnelSchlick(specColor, light, halfVec), fresnel) * ((specPower + 2.0) / 8.0 ) * pow(clamp(dot(normal, halfVec), 0.0, 1.0), specPower) * dotNL;
}

float GeometrySchlickGGX(float dotAB, float k)
{
    return dotAB / (dotAB * (1.0f - k) + k);
}
vec3 ComputeGGX(vec3 specColor, vec3 diffColor, vec3 light, vec3 normal, vec3 halfVec, vec3 view, float gloss, float fresnelFactor, float dotNL)
{
	float roughness = clamp(1.0f - gloss, 0.0f, 1.0f);
	float alpha = roughness * roughness;

	float dotNH = clamp(dot(normal, halfVec), 0.0f, 1.0f);
	float dotNV = clamp(dot(normal, view), 0.0f, 1.0f);

	// NOTE - we're intentionally using UE4's reflectance model as modellers are using tools designed for common engines.

	// Cook-Torrance BRDF Model (Specular Reflection) = Distibution * Fresnel * Geometry / (4*dotNV*dotNL)

	// Distribution Term - Trowbridge-Reitz GGX
	float alphaSqr = alpha * alpha;
	float denom = dotNH * dotNH * (alphaSqr - 1.0f) + 1.0f;
	float distribution = alphaSqr / (PI * denom * denom);

	// Fresnel Term - Fresnel-Schlick
	vec3 fresnel = mix(specColor, FresnelSchlick(halfVec, view, specColor), fresnelFactor);

	// Geometry Term - Schlick-GGX approximation
	float alphaPrime = roughness + 1.0f;
	float k = alphaPrime * alphaPrime / 8.0f;
	// Smith's method: 
	// Microsurfaces block light rays coming in from the light 
	// AND block light rays reflecting to the camera.
	// Hence, model both seperately and multiply.
	float g1vNL = GeometrySchlickGGX(dotNL, k);
	float g1vNV = GeometrySchlickGGX(dotNV, k);
	float geometry = g1vNL * g1vNV;



	vec3 specular = distribution * fresnel * geometry / (4*dotNV*dotNL + 0.0001);
	
	// Diffuse term - Lambertian 
	vec3 kD = vec3(1.0)-fresnel;
	kD *= (vec3(1.0) - specColor);
	vec3 diffuse = kD * diffColor/PI;

	return (specular + diffuse) * dotNL;
}

vec3 computeLighting(vec3 specColor, vec3 diffColor, vec3 light, vec3 normal, vec3 halfVec, vec3 view, float gloss, float fresnelFactor, float dotNL) 
{
	#ifdef FLAG_LIGHT_MODEL_BLINN_PHONG
	return SpecularBlinnPhong(specColor, light, normal, halfVec, 32, fresnelFactor, dotNL);
	#else
	return ComputeGGX(specColor, diffColor, light, normal, halfVec, view, gloss, fresnelFactor, dotNL);
	#endif
}

mat3 localCoordinates(vec3 normal, vec3 tangent_in)
{
// Create basis so we can transform `view` direction into local coord system
// We're not particularly attached to any basis as we have an isotropic roughness, and don't need to align axes with x,y terms.
vec3 tangent = normalize (tangent_in - dot (tangent_in, normal) * normal);
vec3 bitangent = cross(tangent, normal);
mat3 basis = mat3(tangent, bitangent, normal);
return basis;
}